#version 330 core

in vec4 outColor;
in vec3 outPos;
in vec3 outNormal;
in vec2 TexCoords;

uniform vec4 ambientColor;
uniform float ambientIntensity;

uniform vec4 lightColor;
uniform vec4 lightPos;
uniform float lightIntensity;

uniform vec4 cameraPos;

uniform vec4 diffuseColor;
uniform vec4 specularColor;
uniform float roughness;

uniform sampler2D albedoTex;
//uniform sampler2D metalicTex;
//uniform sampler2D normalTex;

out vec4 fragcolor;

#define unity_ColorSpaceDielectricSpec vec4(0.220916301, 0.220916301, 0.220916301, 1.0 - 0.220916301)

vec3 GammaToLinearSpace (vec3 value)
{
    return vec3(pow(value.r, 2.2), pow(value.g, 2.2), pow(value.b, 2.2));
}

vec3 LinearToGammaSpace (vec3 value)
{
    return vec3(pow(value.r, 0.45454545), pow(value.g, 0.45454545), pow(value.b, 0.45454545));
}

vec3 FresnelTerm (vec3 F0, float cosA)
{
	float t = 1-cosA;
	t = t*t;
	t = t*t*(1-cosA);
    return F0 + (1-F0) * t;
};

float GGXTerm (float NdotH, float roughness)
{
    float a2 = roughness * roughness;
    float d = (NdotH * a2 - NdotH) * NdotH + 1.0f;
    return a2 / (d * d + 1e-7f);
};

float SmithJointGGXVisibilityTerm (float NdotL, float NdotV, float roughness)
{

    float a          = roughness;
    float a2         = a * a;

    float lambdaV    = NdotL * sqrt((-NdotV * a2 + NdotV) * NdotV + a2);
    float lambdaL    = NdotV * sqrt((-NdotL * a2 + NdotL) * NdotL + a2);

    return 0.5f / (lambdaV + lambdaL + 1e-5f); 
};

void main()
{
	//vec3 albedo = GammaToLinearSpace(texture(albedoTex, TexCoords).rgb);
	//vec3 value = texture(metalicTex, TexCoords).rgb;
	vec3 albedo = GammaToLinearSpace(outColor.rgb);
	float metallic = 0;
	vec3 diffcolor = unity_ColorSpaceDielectricSpec.a*(1.0 - metallic)*albedo;
	vec3 speccolor = mix(unity_ColorSpaceDielectricSpec.rgb, albedo, metallic);


	vec3 amcolor = GammaToLinearSpace(ambientColor.rgb);
	//vec3 diffcolor = GammaToLinearSpace(diff);
	//vec3 speccolor = GammaToLinearSpace(specularColor.rgb);
	vec3 lightcolor = GammaToLinearSpace(lightColor.rgb*lightIntensity);


	vec3 L = normalize(lightPos.xyz);
	vec3 N = normalize(outNormal);
	vec3 V = normalize(cameraPos.xyz - outPos);
	vec3 H = normalize(L+V);

	float ndl = clamp(dot(N, L), 0.0, 1.0);
	float ndv = clamp(dot(N, V), 0.0, 1.0);
	float ndh = clamp(dot(N, H), 0.0, 1.0);
	float ldh = clamp(dot(L, H), 0.0, 1.0);

	vec3 ambient = amcolor * diffcolor;
	
	vec3 diffuse = ndl * lightcolor.rgb * diffcolor;

	float rough = max(roughness, 0.002);
    float G = SmithJointGGXVisibilityTerm (ndl, ndv, rough);
    float D = GGXTerm (ndh, rough);
	vec3 F = FresnelTerm (speccolor, ldh);
	vec3 specular = ndl * lightcolor.rgb * G * D * F;

	vec3 color = LinearToGammaSpace(ambient + diffuse + specular);
	fragcolor = vec4(color, 1.0f);
}